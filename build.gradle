buildscript {
    dependencies {
        classpath 'org.jacoco:jacoco-maven-plugin:0.8.2'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.8"
        classpath "gradle.plugin.org.gretty:gretty:2.3.1"
        classpath (group: 'com.sahlbach.gradle', name: 'gradle-jetty-eclipse-plugin', version: '1.9.+')
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        // you can also use this for your local cache:
        // mavenLocal()
    }
}

apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: "com.github.sherter.google-java-format"
apply plugin: "org.gretty"

group 'tw.edu.ntu.lowerbound10hours'
version '0.1'

dependencies {
    testCompile "org.testng:testng:6.9.10"
    compile "org.apache.logging.log4j:log4j-api:2.5"
    compile "org.apache.logging.log4j:log4j-core:2.5"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.5.0"
    // https://mvnrepository.com/artifact/org.eclipse.jetty/jetty-server
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.18.v20190429'
    compile 'com.google.guava:guava:27.1-jre'
    compile 'com.hubspot.jinjava:jinjava:2.5.0'
    compile group: 'systems.manifold', name: 'manifold-all', version: '0.55-alpha'
}

repositories {
    mavenCentral()
    // you can also use this for your local cache:
    // mavenLocal()
}

mainClassName = 'tw.edu.ntu.lowerbound10hours.jlask.Main'

defaultTasks 'run'

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

test {
    useTestNG()
    include '**'
}

jacoco {
  toolVersion = '0.8.2'
  reportsDir = file("${buildDir}/reports/jacoco") // this is the default
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
        html.destination = file("${buildDir}/jacocoHtml")
    }
}

// Added to send coverage information to codacy
// see https://github.com/codacy/codacy-coverage-reporter
configurations { codacy }
repositories {
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}
dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}
task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "report",
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
}

// Manifold
tasks.withType(JavaCompile) {
  options.compilerArgs += '-Xplugin:Manifold strings exceptions'
  options.fork = true
}
